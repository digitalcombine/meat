# Object Class and Object Tests
#
# Copyright (c) 2017 Ron R Wills <ron.rwsoft@gmail.com>
#
# This file is part of Meat.
#
# Meat is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# Meat is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with Meat.  If not, see <http://www.gnu.org/licenses/>.

Library import: UnitTest

#==============================================================================
# Class BlockTests
#
#  This class contains various block scenarios to be tested.

Object subClass: BlockTests as: {

  # Simply executes a block context.
	class method executeBlock: block {
	  [block isType: BlockContext] isFalse: {
      self fail: "The context is not a BlockContext object."
    }
	  block execute
	}

  # Returns a block context that executes a context break.
  class method breakBlock {
	  context return: {
		  context break
			Exception throw: "BlockContext failed to break"
		}
	}

  # Returns a nested block context that executes a context break.
	class method nestedBreakBlock {
    context return: {
      self executeBlock: {
        context break
        Exception throw: "Nested BlockContext failed to break"
      }
			Exception throw: "Nested BlockContext failed to break"
		}
	}

  # Returns a block context that executes a context continue.
  class method continueBlock {
	  context return: {
		  context continue
			Exception throw: "BlockContext failed to continue"
		}
	}

  # Returns a nested block context that executes a context continue.
	class method nestedContinueBlock {
    context return: {
      self executeBlock: {
        context continue
        Exception throw: "Nested BlockContext failed to continue"
      }
			Exception throw: "Nested BlockContext failed to continue"
		}
	}

  # Returns a block context that executes a context return.
  class method returnBlock {
    self executeBlock: {
      context return
    }

    Exception throw: "Failed to return from BlockContext"
  }

  # Returns a nested block context that executes a context return.
  class method returnBlockInBlock {
    self executeBlock: {
      self executeBlock: {
        context return
      }
    }

    Exception throw: "Failed to return in nested BlockContexts"
  }

  # Returns a block context that executes a context return result.
	class method returnBlockResult {
    self executeBlock: {
      context return: 10
    }

    Exception throw: "Failed to return result from BlockContext"
  }

  # Returns a nested block context that executes a context return result.
  class method returnBlockInBlockResult {
    self executeBlock: {
      self executeBlock: {
        context return: 10
      }
    }

    Exception throw: "Failed to return result in nested BlockContexts"
  }

}

#==============================================================================
# Class Test.BlockContext

Test.Suite subClass: Test.BlockContext as: {

  method setup {
    self title: "3.4 The BlockContext Class"

    other = ""

    self title: "3.4.1 Method execute" test: {
		  BlockTests executeBlock: {
        self pass
		  }
			self fail: "Failed to execute the block"
    }

    self title: "3.4.2 Method executeOnBreak:" test: {
			# Test that we can capture a break event from a block context.
			failed = [Boolean true]
			block = [BlockTests breakBlock]
			block executeOnBreak: {
			  failed = [Boolean false]
			}
			failed isTrue: {
        self fail: "executeOnBreak failed to capture break"
		  }

			# Test that we can capture a break event from a nested block context.
			failed = [Boolean true]
			block = [BlockTests nestedBreakBlock]
			block executeOnBreak: {
			  failed = [Boolean false]
			}
			failed isTrue: {
        self fail: "executeOnBreak failed to capture break"
		  }

			self pass
    }

		self title: "3.4.3 Method executeOnBreak:onContinue:" test: {
			# Test that we can capture a break event from a block context.
			failed = [Boolean true]
			block = [BlockTests breakBlock]
			block executeOnBreak: {
			  failed = [Boolean false]
			}
			failed isTrue: {
        self fail: "executeOnBreak failed to capture break"
		  }

			# Test that we can capture a break event from a nested block context.
			failed = [Boolean true]
			block = [BlockTests nestedBreakBlock]
			block executeOnBreak: {
			  failed = [Boolean false]
			}
			failed isTrue: {
        self fail: "executeOnBreak failed to capture nested break"
		  }

			self pass
    }

    self title: "3.4.4 Method executeOnContinue:" test: {
			# Test that we can capture a break event from a block context.
			failed = [Boolean true]
			block = [BlockTests continueBlock]
			block executeOnContinue: {
			  failed = [Boolean false]
			}
			failed isTrue: {
        self fail: "executeOnContinue failed to capture continue"
		  }

			# Test that we can capture a break event from a nested block context.
			failed = [Boolean true]
			block = [BlockTests nestedContinueBlock]
			block executeOnContinue: {
			  failed = [Boolean false]
			}
			failed isTrue: {
        self fail: "executeOnContinue failed to capture nested continue"
		  }

			self pass
    }

    self title: "3.4.5 Method localVariables" test: {
      result = [context localVariables]
      [result isType: Integer] isFalse: {
        self fail: "Method localVariables failed to return an integer result"
      }
      [result >= 4] isFalse: {
        self fail: "Method localVariables return a result smaller than 4"
      }
      self pass
    }

    self title: "3.4.6 Method getLocal:" test: {
      [[context getLocal: 0] != self] isTrue: {
        self fail: "Method getLocal failed to return self for index 0"
      }
      [[context getLocal: 1] != class] isTrue: {
        self fail: "Method getLocal failed to return class for index 1"
      }
      [[context getLocal: 2] != context] isTrue: {
        self fail: "Method getLocal failed to return context for index 2"
      }
      self pass
    }

		self title: "3.4.8 Method return" test: {

			BlockTests returnBlock
			BlockTests returnBlockInBlock

		  self pass
		}

    self title: "3.4.9 Method return:" test: {

			[[BlockTests returnBlockResult] == 10] isFalse: {
			  self fail: "BlockContext return: failed to return the expected result"
			}

			[[BlockTests returnBlockInBlockResult] == 10] isFalse: {
			  self fail: "Nested BlockContext return: failed to return the expected result"
			}

		  self pass
		}

		self title: "3.4.10 Method continue" test: {
		  counter = 1
      context repeat: {
        [context isType: BlockContext] isFalse: {
          self fail: "Context is not a BlockContext object."
        }

				[counter == 1] isTrue: {
				  counter = 3
				  context continue
					counter = 2
				}

				[counter == 2] isTrue: {
				  self fail: "BlockContext failed to continue properly"
				}
      }
			self pass
		}

		self title: "3.4.11 Method break" test: {
		  context repeat: {
        [context isType: BlockContext] isFalse: {
          self fail: "Context is not a BlockContext object."
        }

        context break
        self fail: "BlockContext break didn't stop block execution."
      }

			self pass
		}
  }
}
